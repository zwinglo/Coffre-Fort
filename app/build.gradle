apply plugin: 'com.android.application'

android {
    namespace 'com.coffre.fort'
    compileSdk 34

    defaultConfig {
        applicationId "com.coffre.fort"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            res.srcDir "$buildDir/generated/res/app"
        }
    }

    packaging {
        resources {
            // Fix META-INF duplicate file conflicts
            excludes += "META-INF/DEPENDENCIES"

            // Éviter les conflits de ressources META-INF entre jakarta.mail et jakarta.activation
            excludes += 'META-INF/NOTICE.md'
            excludes += 'META-INF/LICENSE.md'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

def mediaDir = file("$rootDir/Media")
def generatedResDir = file("$buildDir/generated/res/app")

tasks.register("copyMediaAssets", Copy) {
    description = "Copies design assets from Media into generated resources"
    group = "Assets"

    into(generatedResDir)

    from(mediaDir) {
        include "Cerradura.png"
        into "drawable"
        rename { "cerradura.png" }
    }

    from("${mediaDir}/Logo/android/res") {
        include "**/*.png"
        rename { name ->
            name
                .replace("Logo_background", "ic_launcher_background")
                .replace("Logo_foreground", "ic_launcher_foreground")
                .replace("Logo_monochrome", "ic_launcher_monochrome")
                .replace("Logo", "ic_launcher")
                .toLowerCase()
        }
    }

    from("${mediaDir}/Logo/android/res") {
        include "**/*.png"
        rename { name ->
            name
                .replace("Logo_background", "ic_launcher_round_background")
                .replace("Logo_foreground", "ic_launcher_round_foreground")
                .replace("Logo_monochrome", "ic_launcher_round_monochrome")
                .replace("Logo", "ic_launcher_round")
                .toLowerCase()
        }
    }
}

tasks.matching { it.name == "preBuild" }.configureEach {
    dependsOn tasks.named("copyMediaAssets")
}

// Éviter les doublons entre kotlin-stdlib 1.8.22 et les vieux modules jdk7/jdk8 en 1.6.21
configurations.all {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.activity:activity:1.8.2'
    implementation 'com.sun.mail:jakarta.mail:2.0.1'
}
